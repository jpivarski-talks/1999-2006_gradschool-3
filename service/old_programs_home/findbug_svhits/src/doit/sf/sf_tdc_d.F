*
* $Id: sf_tdc_d.F,v 1.2 1999/05/28 03:05:02 lyon Exp $
*
* $Log: sf_tdc_d.F,v $
* Revision 1.2  1999/05/28 03:05:02  lyon
* Commented out calls to clever routines or zfiles
*
* Revision 1.1.1.1  1998/02/06 19:11:46  dpp
* DOIT first release.
*
* Revision 1.1.1.1  1997/04/30 12:31:41  clib
* Developmental version of DUET.
*
* Revision 1.1.1.1  1994/10/08 01:00:43  zfiles
* first version of doit in CVS
*
*
#include "sys/CLEO_machine.h"
#include "pilot.h"
*CMZ :  5.54/15 23/03/90  12.38.33  by  Dan Peterson
*CMZ :  2.00/00 08/05/89  15.59.03  by  Dan Peterson
*-- Author :
      SUBROUTINE SF_TDC_D(TDCINP,DISOUT,ISATUR)
C......................................................................
C.
C. SF_TDC_D  :  calculate distance from tdc
C.
C. COMMON    : /STEPCn/

C. CALLS     :
C. CALLED    : SFIND
C. AUTHOR    : D. Peterson
C. VERSION   : 1.00
C. CREATED   : 20-Jul-88
C. LAST MOD  : 06-Oct-88
C.
C. Modification Log.
C......................................................................
#if defined(CLEO_TYPCHK)
      IMPLICIT NONE
#endif
      SAVE
#include "seq/clutil/mesrep.inc"
#include "doit/duseq/runev.inc"
C...... LOCAL VARIABLE DOCUMENTAION
C...... ARGUMENTS
C TDCINP...INPUT OF TDC VALUE
C DISOUT...OUTPUT DISTANCE IN MICROMETERS
C ISATUR...SATURATE?
      INTEGER TDCINP,ISATUR
      REAL DISOUT
C J........LOOP INDEX
C J2.......ANOTHER LOOP INDEX
C J2STRT...STARTING VALUE OF J2
C JSAVE....LOOP INDEX, SAVED
C J2SAVE...LOOP INDEX SAVED
      INTEGER J,J2,J2STRT,JSAVE,J2SAVE
C....... FOR RUN NUMBER
C RUNN.....RUN NUMBER
C REALDM...REAL DUMMY NUMBER
      INTEGER RUNN,IOS
      REAL REALDM
C...... STUFF FOR THE PROCEDURE
C IRETRN...RETURN FOR PROCEDURE
C TDCPRO...INPUT TDC TO PROCEDURE
C POSITN...RETURNED RELATIVE POSITION FROM PROCEDURE
C XBIN1....BIN NUMBER, IN REFERENCE HISTOGRAM, OF INPUT
C IBIN1....BIN NUMBER, INTEGER OF XBIN1, ROUNDED, NOT TRUNCATED
C BINDIF...RELATIVE POSITION IN THE BIN, XBIN1-IBIN1
      INTEGER IRETRN,TDCPRO,IBIN1
      REAL POSITN,XBIN1,BINDIF
C...... THESES ARE THE VARIABLES THAT STORE THE RUN DEPENDENCE
C RNSTRT...RUN NUMBER TO START USING THIS SET OF DATA
C TDCST....___MIDDLE___(NO:LOW END)   OF FIRST TDC STORED BIN ,TEMP
C TDCIT.....TDC STORED BIN INCREMENT , TEMP
C IVALT....TDC STORED HISTOGRAM VALUE , TEMP
C TDCI0T...TDC INPUT ZERO
C TDCIMT...TDC INPUT MAXIMUM
C DIST0T...DISTANCE AT TDCI0T, IN MICROMETERS
C DISTMT...DISTANCE AT TDCIMT
C FERMWT...FERMI FUNCTION DROP OFF OF EFFICIENCY, IN MICRONS
C DSSATT...DISTANCE TO SATURATE
      INTEGER RNSTRT(2),TDCST(2),TDCIT(2)
      INTEGER IVALT(40,2)
      INTEGER TDCI0T(2),TDCIMT(2)
      REAL DIST0T(2),DISTMT(2),FERMWT(2),DSSATT(2)
C
      DATA RNSTRT(1),TDCST(1),TDCIT(1) /    0,1600,100/
      DATA RNSTRT(2),TDCST(2),TDCIT(2) /30883,1100,100/
      DATA TDCI0T(1),TDCIMT(1)/1720,3500/
      DATA TDCI0T(2),TDCIMT(2)/1370,3250/
      DATA DIST0T(1),DISTMT(1),FERMWT(1),DSSATT(1)
     1                        /0.,5500.,   1.,7000./
      DATA DIST0T(2),DISTMT(2),FERMWT(2),DSSATT(2)
     1                        /0.,6300.,1100.,7000./
      DATA IVALT/
     1   3,19,32,24,18,13,10, 8, 6, 5,
     1   4, 4, 4, 3, 4, 4, 3, 3, 3, 3,
     1   3, 3, 1,17*0,
     2    359,  888, 6947,17586,14983,10836, 8286, 6665, 5249, 4239,
     2   3622, 3426, 3050, 2731, 2613, 2612, 2445, 2458, 2492, 2370,
     2   2354, 2401, 2294, 2278, 1800, 1400, 1000,  13*0/
C
C...... THESE ARE THE VARIABLES INTO WHICH THE RUN DEPENDENCE IS COPIED
C TDCSU....___MIDDLE___ (NO:LOW END)   OF FIRST TDC STORED BIN ,USED
C TDCIU....TDC STORED BIN INCREMENT , USED
C IVALU....TDC STORED HISTOGRAM VALUE , USED
C TDCI0U...TDC INPUT ZERO
C TDCIMU...TDC INPUT MAXIMUM
C DIST0U...DISTANCE AT TDCI0U, IN MICROMETERS
C DISTMU...DISTANCE AT TDCIMU
C FERMWU...FERMI FUNCTION DROP OFF OF EFFICIENCY, IN MICRONS
C DSSATU...DISTANCE TO SATURATE
      INTEGER TDCSU,TDCIU
      INTEGER IVALU(40)
      INTEGER TDCI0U,TDCIMU
      REAL DIST0U,DISTMU,FERMWU,DSSATU
C
C...... THESE ARE CALCUALTED FROM THE RUN DEPENDENT VARIABLES
C RELFR0...RELATIVE FRACTION 0
C RELFRM...RELATIVE FRACTION MAXIMUM
C RELFRD...RELFRM-RELRF0
C DISTD....DISTMU=DIST0U
C IVALS....TDC STORED HISTOGRAM RUNNING SUM
C DISSAV...TEMP:RELATIVE POSITION FOR TDC INPUT/
C          FINALLY: DISTANCE THAT WOULD HAVE THE SAME RELATIVE POSTION
      REAL RELFR0,RELFRM,RELFRD,DISTD
      INTEGER IVALS(40)
      REAL DISSAV(0:4095)
C...... THESE ARE THE HISTOGRAMS OF THE DAUGHTER DISTRIBUTION
C MHOUT....MAXIMUM SIZE OF THE DAUGHTER DISTRIBUTION
C NHOUT....USED SIZE OF THE DAUGHTER DISTRIBUTION
C HOUTDF...HISTOGRAM OUTPUT DIFFERENTIAL  (ANALOGOUS TO IVALU)
C HOUTSM...HISTOGRAM OUTPUT SUM           (ANALOGOUS TO IVALS)
C HOUTVL...HISTOGRAM OUTPUT "X" VALUE
C HOUTNM...NORMILIZATION OF HOUTSM
C DELHVL...DELTA HISTOGRAM OUTPUT "X" VALUE
      INTEGER MHOUT
      PARAMETER (MHOUT=150)
      INTEGER NHOUT
      REAL HOUTDF(MHOUT),HOUTSM(MHOUT),HOUTVL(MHOUT)
      REAL DELHVL,HOUTNM
C.......
C IFIRST...=1 IF THIS IS FIRST CALL
      INTEGER IFIRST
      DATA IFIRST/1/
C----------------------Executable code starts here---------------------
C
      IF(IFIRST.EQ.0)GO TO 91
      IFIRST=0
C
      RUNN = current_run
      JSAVE=1
      DO 3 J=2,2,-1
      JSAVE=J
      IF(RUNN.GE.RNSTRT(J))GO TO 4
3     CONTINUE
4     CONTINUE
C
      TDCSU=TDCST(JSAVE)
      TDCIU=TDCIT(JSAVE)
      DO 5 J=1,40
5     IVALU(J)=IVALT(J,JSAVE)
      TDCI0U=TDCI0T(JSAVE)
      TDCIMU=TDCIMT(JSAVE)
      DIST0U=DIST0T(JSAVE)
      DISTMU=DISTMT(JSAVE)
      FERMWU=FERMWT(JSAVE)
      DSSATU=DSSATT(JSAVE)
      DISTD=DISTMU-DIST0U
C
C BUILD THE TDC HISTOGRAM SUM
C  THAT IS, SUM UP TO LOW END, BUT "VALUE" IS BEYOND LOW END
      IVALS(1)=0.
      DO 17 J=2,40
      IVALS(J)=IVALS(J-1)+IVALU(J-1)
17    CONTINUE
C
C BUILD THE OUTPUT HISTOGRAM , FROM DIST0U,DISTMU,FERMWU
C ELEMENT#1     HAS LOW END AT DIST0U
C ELEMENT#101  HAS LOW END AT DISTMU
C BUILD THE OUTPUT HISTOGRAM SUM
C  THAT IS, SUM UP TO LOW END, BUT "VALUE" IS BEYOND LOW END
      NHOUT=100.*(DISTD+2.*FERMWU)/DISTD +1.5
      IF(NHOUT.LE.MHOUT)GO TO 19
      WRITE(CHMESS,9005,IOSTAT=IOS)NHOUT
9005  FORMAT(' SF_TDC_D: NHOUT=',I6,' INSUFFICIENT SPACE')
      CALL MESLOG('SF_TDC_D',1,MSERRO)
      NHOUT=MHOUT
19    DO 23 J=1,NHOUT
C VALUE AT CENTER OF BIN, FOR CALCULATION OF RATE
      HOUTVL(J)=DIST0U+(1.*J-0.5)/100.*DISTD
      HOUTDF(J)=1./(1.+EXP((HOUTVL(J)-DISTMU)/FERMWU))
      IF(HOUTVL(J).LT.0.)HOUTDF(J)=0.
C NOW SET TO VALUE AT LOW EDGE OF THE BIN
      HOUTVL(J)=HOUTVL(J)  -  .5/100.*DISTD
      IF(J.GT.1)GO TO 21
      HOUTSM(1)=0.
      GO TO 23
21    HOUTSM(J)=HOUTSM(J-1)+HOUTDF(J-1)
23    CONTINUE
C NORMALIZE
      HOUTNM=HOUTSM(101)
      DO 25 J=1,NHOUT
      HOUTDF(J)=HOUTDF(J)/HOUTNM
25    HOUTSM(J)=HOUTSM(J)/HOUTNM
      DELHVL=HOUTVL(4)-HOUTVL(3)
C
C
C BUILD UP THE DISTANCE SAVED CHART FOR ALL POSSIBLE TDC INPUT
C FIRST USE DISSAV TO STORE THE RELATIVE POSITION FOR TDC INPUT
      DO 37 J=0,4095
C THIS IS THE FLOATING POINT BIN NUMBER
      XBIN1=(1.*J-1.*TDCSU)/TDCIU +1.
      IF(XBIN1.LT. 1.0)XBIN1= 1.0
      IF(XBIN1.GT.40.99999)XBIN1=40.99999
C THIS IS THE INTEGER BIN , LOW END OF THE BIN
      IBIN1=XBIN1
C THIS IS THE DISTANCE OF XBIN1 FROM IBIN1
      BINDIF=XBIN1-1.*IBIN1
C THIS IS THE FRACTION POSITION RELATIVE TO THE FULL SUM
      POSITN=(1.*IVALS(IBIN1)+BINDIF*IVALU(IBIN1))
     1            /(1.*IVALS(40))
      IF(POSITN.LT.0.)POSITN=0.
      IF(POSITN.GT.1.)POSITN=1.
37    DISSAV(J)=POSITN
C
C RENORMALIZE SO THAT INPUT =TDCI0U  YIELDS DISTANCE=DIST0U
C    AND         THAT INPUT =TDCIMU  YIELDS DISTANCE=DISTMU
      RELFR0=DISSAV(TDCI0U)
      RELFRM=DISSAV(TDCIMU)
      RELFRD=RELFRM-RELFR0
      J2STRT=1
      DO 41 J=0,4095
      DISSAV(J)=(DISSAV(J)-RELFR0)/RELFRD
41    CONTINUE
      DO 47 J=0,4095
C
C FIND THE HOUTSM THAT GIVES THIS DISSAV
C  THAT IS, SUM UP TO LOW END, BUT "VALUE" IS BEYOND LOW END
C NEED LAST HOUTSM(J2) THAT IS BELOW DISSAV(J), THEN EXTRAPOLATE UP
      IF(J2STRT.LT.1)J2STRT=1
      J2SAVE=J2STRT
      DO 43 J2=J2STRT,NHOUT
      IF(HOUTSM(J2).GT.DISSAV(J))GO TO 45
      J2SAVE=J2
43    CONTINUE
45    CONTINUE
      DISSAV(J)=HOUTVL(J2SAVE)+(DISSAV(J)-HOUTSM(J2SAVE))
     1                          /HOUTDF(J2SAVE)*DELHVL
      J2STRT=J2SAVE-1
47    CONTINUE
C
C
C
      WRITE(CHMESS,9001,IOSTAT=IOS)RUNN,JSAVE
9001  FORMAT(' SF_TDC_D: COMPLETED INITIALIZATION:',
     1       ' RUN#',I6,' GROUP:',I2)
      CALL MESLOG('SF_TDC_D',1,MSSUCC)
      WRITE(CHMESS,9002,IOSTAT=IOS)TDCI0U,RELFR0,DISSAV(TDCI0U)
9002  FORMAT(9X,' TDC=',I4,' REL FRACTION=',F6.3,'  TIME=',F7.1,' MICM')
      CALL MESLOG('SF_TDC_D',1,MSSUCC)
      WRITE(CHMESS,9002,IOSTAT=IOS)TDCIMU,RELFRM,DISSAV(TDCIMU)
      CALL MESLOG('SF_TDC_D',1,MSSUCC)
C
C THIS IS THE REGULAR CODE
91    J=TDCINP
      IF(J.GE.0)GO TO 92
      WRITE(CHMESS,9007,IOSTAT=IOS)TDCINP
9007  FORMAT(' INPUT ( SHOULD BE RAW TDC) IS STUPID, =',I10)
      CALL MESLOG('SF_TDC_D',1,MSERRO)
      J=0
      GO TO 95
92    IF(J.LT.4096)GO TO 95
      WRITE(CHMESS,9007,IOSTAT=IOS)TDCINP
      CALL MESLOG('SF_TDC_D',1,MSERRO)
      J=4095
95    DISOUT=DISSAV(J)
C
      IF(ISATUR.NE.1)GO TO 98
      IF(DISOUT.LT.0.)DISOUT=0.
      IF(DISOUT.GT.DSSATU)DISOUT=DSSATU
98    CONTINUE
C
      RETURN
      END
